<?xml version="1.0" encoding="utf-8"?>
<!-- multiple-output-files.xsl -->
<xsl:stylesheet version="2.0"
  xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
  xmlns:redirect="org.apache.xalan.xslt.extensions.Redirect"
  extension-element-prefixes="redirect">

  <xsl:output method="html"/>

  <xsl:template match="/">
    <html>
      <head>
        <xsl:comment>
          <xsl:text>Results generated by </xsl:text>
          <xsl:value-of select="system-property('xsl:vendor')"/>
        </xsl:comment>
        <title><xsl:value-of select="book/title"/></title>
      </head>
      <body style="font-family: sans-serif;">
        <h1><xsl:value-of select="book/title"/></h1>
        <p>Here are some interesting XSLT topics:</p>
        <ul>
          <xsl:choose>
            <xsl:when test="element-available('xsl:result-document')">
              <xsl:apply-templates select="book/chapter" mode="separate-files"/>
            </xsl:when>
            <xsl:when test="element-available('redirect:write')">
              <xsl:apply-templates select="book/chapter" mode="separate-files"/>
            </xsl:when>
            <xsl:otherwise>
              <xsl:for-each select="book/chapter">
                <li>
                  <a href="{concat('#chapter', position())}">
                    <xsl:value-of select="title"/>
                  </a>
                </li>
              </xsl:for-each>
            </xsl:otherwise>
          </xsl:choose>
        </ul>

        <xsl:for-each select="book/chapter">
          <xsl:result-document method="html" 
            include-content-type="no"
            href="{concat('chapter', position(), '.html')}">
            <xsl:apply-templates select="." mode="create-html-file"/>

            <xsl:fallback>
              <redirect:write 
                select="concat('chapter', position(), '.html')">
                <xsl:apply-templates select="." mode="create-html-file"/>

                <xsl:fallback>
                  <a name="{concat('chapter', position())}"/>
                  <h1>
                    <xsl:value-of select="title"/>
                  </h1>
                  <xsl:apply-templates select="*[position() > 1]"/>
                </xsl:fallback>
              </redirect:write>
            </xsl:fallback>
          </xsl:result-document>
        </xsl:for-each>
      </body>
    </html>
  </xsl:template>

  <xsl:template match="chapter" mode="separate-files">
    <li>
      <a href="{concat('chapter', position(), '.html')}">
        <xsl:value-of select="title"/>
      </a>
    </li>
  </xsl:template>

  <xsl:template match="chapter" mode="create-html-file">
    <html>
      <head>
        <title><xsl:value-of select="title"/></title>
      </head>
      <body style="font-family: sans-serif;">
        <h1><xsl:value-of select="title"/></h1>
        <xsl:apply-templates select="*[position() > 1]"/>
      </body>
    </html>
  </xsl:template>

</xsl:stylesheet>
